import java.text.SimpleDateFormat

plugins {
    id 'java'
    id 'net.nemerosa.versioning' version '2.6.1'
}

group 'com.github.antoinejt'

def lastUpdate = 'October 10th 2019'

def majorVersionNumber = '0'
def minorVersionNumber = '5'
def patchVersionNumber = '0'

def versionNumber = "${majorVersionNumber}.${minorVersionNumber}.${patchVersionNumber}"
def internalVersionNumber = "${versionNumber}${versioning.info.branch != 'master' ? '-DEVELOP' : ''}"

version "${versionNumber}-${versioning.info.full}"

sourceCompatibility = 1.8

jar {
    manifest {
        attributes(
                'Main-Class': 'com.github.antoinejt.jasc.Main',
                'Built-By': System.properties['user.name'],
                'Last-Update': lastUpdate,
                'Version': internalVersionNumber,
                'Build-Timestamp': new SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
                'Build-Revision': versioning.info.commit,
                'Created-By': "Gradle ${gradle.gradleVersion}",
                'Build-Jdk': "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
                'Build-OS': "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}"
        )
    }
}

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
}
